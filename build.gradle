plugins {
    id 'scala'
    id 'application'
    id 'com.github.johnrengelman.shadow' version '6.0.0'
}

version '0.0.0'
mainClassName = 'org.riskala.controller.Main'
sourceCompatibility = 1.8

repositories {
    jcenter()
    mavenCentral()
}

task copyFrontendToResources(type: Copy) {
    dependsOn ':src:vuejs:npm_run_build'
    from "$projectDir/src/vuejs/dist/"
    into "$projectDir/src/main/resources/static"
}

jar {
    dependsOn copyFrontendToResources
}

processResources.mustRunAfter(copyFrontendToResources)

shadowJar {
  append 'reference.conf'
}

def scalaMajor = '2.12'
def scalaMinor = '1'
def akkaVersion = '2.6.8'
def akkaHttpVersion = '10.2.0'

dependencies {

    implementation(
        [group: "org.scala-lang", name: "scala-library", version: "$scalaMajor.$scalaMinor"],
        [group: "com.typesafe.akka", name: "akka-actor-typed_$scalaMajor", version: "$akkaVersion"],
        [group: "com.typesafe.akka", name: "akka-stream_$scalaMajor", version: "$akkaVersion"],
        [group: "com.typesafe.akka", name: "akka-stream-typed_$scalaMajor", version: "$akkaVersion"],
        [group: "com.typesafe.akka", name: "akka-http_$scalaMajor", version: "$akkaHttpVersion"],
        [group: "com.typesafe.akka", name: "akka-http-spray-json_$scalaMajor", version: "$akkaHttpVersion"],
        [group: "io.spray", name: "spray-json_$scalaMajor", version: "1.3.5"],
        [group: "com.pauldijou", name: "jwt-core_$scalaMajor", version: "4.3.0"],
        [group: "io.argonaut", name: "argonaut_$scalaMajor", version: "6.3.0"],
        [group: "org.slf4j", name: "slf4j-simple", version: "1.7.25"]
    )

    testImplementation(
        [group: "org.scalatestplus", name: "junit-4-12_$scalaMajor", version: "3.1.2.0"],
        [group: "com.typesafe.akka", name: "akka-actor-testkit-typed_$scalaMajor", version: "$akkaVersion"],
        [group: "com.typesafe.akka", name: "akka-stream-testkit_$scalaMajor", version: "$akkaVersion"],
        [group: "com.typesafe.akka", name: "akka-http-testkit_$scalaMajor", version: "$akkaHttpVersion"]
    )

    testRuntimeOnly(
        [group: "org.scala-lang.modules", name: "scala-xml_$scalaMajor", version: "1.2.0"]
    )
}